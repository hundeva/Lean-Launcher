buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.3'
        classpath 'com.google.gms:google-services:3.2.0'
    }
}

allprojects {
    ext {
        version_name = "1.1.7"
        version_code = 22

        compile_sdk_version = 27
        min_sdk_version = 21
        target_sdk_version = 27

        support_lib_version = '27.1.0'
        gms_version = '11.8.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionName version_name
        versionCode version_code
        setProperty("archivesBaseName", "LeanLauncher-$version_name-$version_code")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        lean {
            storeFile file(LEAN_RELEASE_KEY_STORE_FILE)
            storePassword LEAN_RELEASE_KEY_STORE_PASSWORD
            keyAlias LEAN_RELEASE_KEY_ALIAS
            keyPassword LEAN_RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            signingConfig signingConfigs.lean
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.flags'
        }

        optimized {
            initWith debug
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.flags'
        }

        release {
            minifyEnabled true
            signingConfig signingConfigs.lean
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard.flags'
        }
    }

    productFlavors {
        play {
            applicationId 'com.hdeva.launcher'
            testApplicationId 'com.android.launcher3.tests'
            resValue "string", "xmlApplicationId", applicationId
            resValue "string", "settingsApplicationId", "package:$applicationId"
            flavorDimensions 'default'
        }

        aosp {
            applicationId 'com.google.android.apps.nexuslauncher'
            testApplicationId 'com.android.launcher3.tests'
            resValue "string", "xmlApplicationId", applicationId
            resValue "string", "settingsApplicationId", "package:$applicationId"
            flavorDimensions 'default'
        }

        l3go {
            applicationId 'com.google.android.apps.nexuslauncher'
            testApplicationId 'com.android.launcher3.tests'
            resValue "string", "xmlApplicationId", applicationId
            resValue "string", "settingsApplicationId", "package:$applicationId"
            flavorDimensions 'default'
        }
    }
    sourceSets {
        main {
            res.srcDirs = ['res']
            java.srcDirs = ['src']
            aidl.srcDirs = ['src']
            assets.srcDirs = ['assets']
            manifest.srcFile 'AndroidManifest-common.xml'
            proto {
                srcDir 'protos/'
                srcDir 'proto_overrides/'
                srcDir 'proto_pixel/'
            }
        }

        androidTest {
            res.srcDirs = ['tests/res']
            java.srcDirs = ['tests/src']
            manifest.srcFile "tests/AndroidManifest-common.xml"
        }

        play {
            java.srcDirs = ['src_flags']
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTestPlay {
            manifest.srcFile "tests/AndroidManifest.xml"
        }

        aosp {
            java.srcDirs = ['src_flags']
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTestAosp {
            manifest.srcFile "tests/AndroidManifest.xml"
        }

        l3go {
            res.srcDirs = ['go/res']
            java.srcDirs = ['go/src_flags']
            // Note: we are using the Launcher3 manifest here because the gradle manifest-merger uses
            // different attributes than the build system.
            manifest.srcFile "AndroidManifest.xml"
        }

        androidTestL3go {
            manifest.srcFile "tests/AndroidManifest.xml"
        }
    }

    lintOptions {
        checkReleaseBuilds false
    }
}

repositories {
    mavenCentral()
    jcenter()
    google()
}

dependencies {
    implementation "com.android.support:support-v4:$support_lib_version"
    implementation "com.android.support:support-dynamic-animation:$support_lib_version"
    implementation "com.android.support:recyclerview-v7:$support_lib_version"
    implementation "com.android.support:palette-v7:$support_lib_version"
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-7'

    implementation "com.google.firebase:firebase-core:$gms_version"
    implementation "com.google.firebase:firebase-crash:$gms_version"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation "org.mockito:mockito-core:1.9.5"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'
    androidTestImplementation 'com.android.support.test:runner:0.5'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    androidTestImplementation "com.android.support:support-annotations:$support_lib_version"
}

protobuf {
    // Configure the protoc executable
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'

        generateProtoTasks {
            all().each { task ->
                task.builtins {
                    remove java
                    javanano {
                        option "java_package=launcher_log_extension.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_log.proto|com.android.launcher3.userevent.nano"
                        option "java_package=launcher_dump.proto|com.android.launcher3.model.nano"
                        option "java_package=search.proto|com.google.android.apps.nexuslauncher.search.nano"
                        option "java_package=smartspace.proto|com.google.android.apps.nexuslauncher.smartspace.nano"
                        option "enum_style=java"
                    }
                }
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
